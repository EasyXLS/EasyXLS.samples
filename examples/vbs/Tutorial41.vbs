    '=============================================================================
    ' Tutorial 41
    '
    ' This tutorial shows how to convert XML spreadsheet to Excel in VBScript. The
	' XML Spreadsheet generated by Tutorial 32 is imported, some data is modified
	' and after that is exported as Excel file.
    '=============================================================================
    
    WScript.StdOut.WriteLine("Tutorial 41" & vbcrlf & "-----------" & vbcrlf)
    
	' Create an instance of the class used to import/export Excel files
	Set workbook = CreateObject("EasyXLS.ExcelDocument")
    
    ' Import XML Spreadsheet file
    WScript.StdOut.WriteLine("Reading file: C:\Samples\Tutorial32.xml" & vbcrlf)
    If (workbook.easy_LoadXMLSpreadsheetFile_2("C:\Samples\Tutorial32.xml")) Then
    		
		' Get the table of data from the second sheet and add some data in cells (optional step)
		Set xlsTable = workbook.easy_getSheetAt(1).easy_getExcelTable()

		xlsTable.easy_getCell_2("A1").setValue("Data added by Tutorial41")

		For column=0 To 4
			xlsTable.easy_getCell(1, column).setValue("Data " & (column + 1))
		Next
        
        ' Export Excel file
        Wscript.StdOut.WriteLine(vbcrlf & "Writing file: C:\Samples\Tutorial41 - convert XML spreadsheet to Excel.xlsx")
        workbook.easy_WriteXLSXFile ("C:\Samples\Tutorial41 - convert XML spreadsheet to Excel.xlsx")
    
		' Confirm conversion of XML Spreadsheet to Excel
		Dim sError
		sError = workbook.easy_getError()
		If sError = "" Then
		WScript.StdOut.WriteLine(vbcrlf & "File successfully created.")
		Else
			WScript.StdOut.WriteLine(vbcrlf & "Error: " & sError)
		End If    
    Else
        Wscript.StdOut.WriteLine("Error reading file C:\Samples\Tutorial32.xml" & vbcrlf & workbook.easy_getError())
    End If
    
	' Dispose memory
	workbook.Dispose
    
    Wscript.StdOut.Write(vbcrlf & "Press Enter to exit ...")
    Wscript.StdIn.ReadLine()
